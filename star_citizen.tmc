// -----------------------------------------------------------------------------
// The MIT License (MIT)
// 
// Copyright (c) 2014-2015 Alex McLain
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
// -----------------------------------------------------------------------------


// TODO: Remap mic switch to comms and move shields (to CMS?).
//       Mic up/down/left/right used for:
//          In-Game, TeamSpeak, Future, Future
//       Mic push:
//          Voice Attack


// TARGET Script
include "target.tmh"
include "usb_keys.tmh"

// User-editiable axis curves.
int x_curve = 0;
int y_curve = 0;
int throttle_curve = 0;
int strafe_x_curve = 0;
int strafe_y_curve = 0;
int aim_x_curve = 0;
int aim_y_curve = 0;

// Delay in milliseconds to determine a "long" button press for commands
// that have a short/long option.
int long_delay_amount = 500;

// Key Bindings
define SC_ROLL_AXIS                     DX_X_AXIS
define SC_PITCH_AXIS                    DX_Y_AXIS
define SC_THROTTLE_AXIS                 DX_THROTTLE_AXIS

define SC_WEAPON_GROUP_1                DX1
define SC_WEAPON_GROUP_2                DX6
define SC_WEAPON_GROUP_3                DX3
define SC_WEAPON_GROUP_4                DX13 // TODO: Add to XML
define SC_LAUNCH_MISSILE                DX2
define SC_IFCS_TOGGLE                   DX5
// define SC_COMSTAB_TOGGLE                DX5
// define SC_GSAFE_TOGGLE                  DX16
define SC_LAUNCH_COUNTERMEASURE         DX15
define SC_SCOREBOARD                    DX4
define SC_TARGET_NEAREST_HOSTILE        DX7
define SC_TARGET_HOSTILE_FWD            DX9
define SC_PIN_FOCUSED_TARGET            DX11
define SC_TARGET_ALL_FWD                DX8
define SC_TARGET_ALL_BACK               DX10
define SC_MISSILE_CYCLE_FWD             DX12
define SC_MISSILE_CYCLE_BACK            DX14
define SC_POWER_GROUP_1                 USB_1
define SC_POWER_GROUP_2                 USB_2
define SC_POWER_GROUP_3                 USB_3
define SC_POWER_GROUP_RESET             USB_4
define SC_RADAR_ZOOM                    DX18
define SC_COUNTERMEASURE_CYCLE_FWD      DX17
define SC_SHIELD_FWD                    DX22
define SC_SHIELD_AFT                    DX24
define SC_SHIELD_LEFT                   DX25
define SC_SHIELD_RIGHT                  DX23
define SC_SHIELD_RESET                  DX26
define SC_SHIELD_BOOST_RECHARGE         USB_C
define SC_BRAKE                         DX20
define SC_AFTERBURNER                   DX32
define SC_STRAFE_UP                     DX19
define SC_STRAFE_DOWN                   DX27
define SC_STRAFE_LEFT                   DX28
define SC_STRAFE_RIGHT                  DX29
define SC_AIM_RESET                     USB_RIGHT_SHIFT
define SC_GIMBAL_LOCK                   USB_LEFT_ALT
define SC_HUD_OVERVIEW                  USB_F1
define SC_HUD_WEAPONS                   USB_F2
define SC_HUD_POWER                     USB_F3
define SC_HUD_SHIELDS                   USB_F4
define SC_IFCS_DECOUPLE                 DX21
define SC_EJECT                         DX30
define SC_USE_DOCKING                   DX31
define SC_RUNNING_LIGHTS                USB_O
define SC_CABIN_LIGHTS                  USB_O
define SC_HUD_TAB_1                     USB_F1
define SC_HUD_TAB_2                     USB_F2
define SC_HUD_TAB_3                     USB_F3
define SC_HUD_TAB_4                     USB_F4
define SC_HUD_TAB_5                     USB_F5
define SC_HUD_TAB_6                     USB_F6
define SC_HUD_TAB_7                     USB_F7
define SC_HUD_TAB_8                     USB_F8
define SC_HUD_TAB_9                     USB_F9
define SC_HUD_TAB_10                    USB_F10
define SC_HUD_MAXIMIZE                  USB_F11
define SC_HUD_CONFIRM                   USB_RETURN
define SC_HUD_LEFT_PANEL_UP             USB_UP_ARROW
define SC_HUD_LEFT_PANEL_DOWN           USB_DOWN_ARROW
define SC_HUD_LEFT_PANEL_LEFT           USB_LEFT_ARROW
define SC_HUD_LEFT_PANEL_RIGHT          USB_RIGHT_ARROW
define SC_HUD_MODE_FWD                  USB_SEMICOLON
define SC_HUD_MODE_BACK                 USB_APOSTROPHE
define VOICE_ATTACK                     L_ALT + USB_F2

define LED_INTENSITY_LEVEL_0            0
define LED_INTENSITY_LEVEL_1            43
define LED_INTENSITY_LEVEL_2            86
define LED_INTENSITY_LEVEL_3            129
define LED_INTENSITY_LEVEL_4            172
define LED_INTENSITY_LEVEL_5            215


int tg1_weapon = SC_WEAPON_GROUP_1;
int tg2_weapon = SC_WEAPON_GROUP_2;

int main()
{
    Configure(&HCougar, MODE_EXCLUDED);
    Configure(&T16000, MODE_EXCLUDED);
    
    if(Init(&EventHandle)) return 1;
    
    ActKey(PULSE + KEYON + LED(&Throttle, LED_INTENSITY, LED_INTENSITY_LEVEL_1));
    
    EXEC("update_leds();");
    
    SetKBRate(32, 50);
    SetKBLayout(KB_ENG);
    SetShiftButton(0, 0, 0, 0, 0, 0);
    
    // -------------
    // Joystick Axes
    // -------------
    MapAxis(&Joystick, JOYX, SC_ROLL_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
    SetSCurve(&Joystick, JOYX, 0, 0, 0, x_curve, 0);
    MapAxis(&Joystick, JOYY, SC_PITCH_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
    SetSCurve(&Joystick, JOYY, 0, 0, 0, y_curve, 0);
    
    // -------------
    // Throttle Axes
    // -------------
    
    // Throttle
    MapAxis(&Throttle, THR_RIGHT, SC_THROTTLE_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
    SetSCurve(&Throttle, THR_RIGHT, 0, 0, 0, throttle_curve, 0);
    
    // Slew Control - Strafe
    MapAxis(&Throttle, SCX, DX_XROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
    SetSCurve(&Throttle, SCX, 0, 0, 0, strafe_x_curve, 0);
    MapAxis(&Throttle, SCY, DX_YROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
    SetSCurve(&Throttle, SCY, 0, 0, 0, strafe_y_curve, 0);
    
    // TODO: Slew Control - Aim
    // MapAxis(&Throttle, SCX, DX_Z_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
    // SetSCurve(&Throttle, SCX, 0, 0, 0, aim_x_curve, 0);
    // MapAxis(&Throttle, SCY, DX_ZROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
    // SetSCurve(&Throttle, SCY, 0, 0, 0, aim_y_curve, 0);
    
    // ----------------
    // Joystick Buttons
    // ----------------
    
    // Trigger
    // Selectable with trim hat. -----------------------------------------------
    // Short = TG1, Long = TG2
    //       H1U - Weapon Group 1
    //       H1D - Weapon Group 2
    //       H1L - Weapon Group 3
    //       H1R - Weapon Group 4
    // Swappable with pinky switch. --------------------------------------------
    //       FWD - TODO: Expose group 3/4?
    //       MID - Normal:   TG1=Group1, TG2=Group2
    //       AFT - Reversed: TG1=Group2, TG2=Group1
    // -------------------------------------------------------------------------
    // NOTE:
    //       Hat always sets TG1 on short and TG2 on long.
    //       Throttle LED's always show TG1 weapon.
    //       Pinky switch swaps the values in tg1_weapon/tg2_weapon.
    MapKey(&Joystick, TG1, EXEC("weapon_selector();"));
    MapKey(&Joystick, TG2, EXEC("weapon_selector();"));
    
    MapKeyR(&Joystick, TG1, EXEC("weapon_selector();"));
    MapKeyR(&Joystick, TG2, EXEC("weapon_selector();"));
    
    // Reevaluate logic when trim hat changes state.
    MapKey(&Joystick, H1U, TEMPO(
        EXEC("set_tg1_weapon(SC_WEAPON_GROUP_1);"),
        EXEC("set_tg2_weapon(SC_WEAPON_GROUP_1);"),
        long_delay_amount
    ));
    MapKey(&Joystick, H1D, TEMPO(
        EXEC("set_tg1_weapon(SC_WEAPON_GROUP_2);"),
        EXEC("set_tg2_weapon(SC_WEAPON_GROUP_2);"),
        long_delay_amount
    ));
    MapKey(&Joystick, H1L, TEMPO(
        EXEC("set_tg1_weapon(SC_WEAPON_GROUP_3);"),
        EXEC("set_tg2_weapon(SC_WEAPON_GROUP_3);"),
        long_delay_amount
    ));
    MapKey(&Joystick, H1R, TEMPO(
        EXEC("set_tg1_weapon(SC_WEAPON_GROUP_4);"),
        EXEC("set_tg2_weapon(SC_WEAPON_GROUP_4);"),
        long_delay_amount
    ));
    
    // Reevaluate logic when pinky switch changes state.
    MapKey(&Throttle, PSM, EXEC("swap_weapons();"));
    MapKey(&Throttle, PSB, EXEC("swap_weapons();"));
    
    // MMCB
    // MapKey(&Joystick, S1, TEMPO(
    //     SC_COMSTAB_TOGGLE, // Short
    //     SC_GSAFE_TOGGLE,   // Long
    //     long_delay_amount
    // )); 
    // 
    // Switched to single tap (Comstab) / double tap (GSafe) due to SC
    // changing the button from toggle to momentary if it's held down.
    // See bug report:
    // https://forums.robertsspaceindustries.com/discussion/229973/controls-input-coupled-gsafe-comstab-toggles-don-t-stay-latched#latest
    MapKey(&Joystick, S1, SC_IFCS_TOGGLE);
    
    // Weapon Release
    MapKey(&Joystick, S2, SC_LAUNCH_MISSILE);
    
    // NWS
    MapKey(&Joystick, S3, SC_LAUNCH_COUNTERMEASURE);
    
    // Paddle Switch
    MapKey(&Joystick, S4, SC_SCOREBOARD);
    
    // TMS Hat
    MapKey(&Joystick, H2U, TEMPO(
        SC_TARGET_NEAREST_HOSTILE, // Short
        SC_PIN_FOCUSED_TARGET,     // Long
        long_delay_amount
    ));
    MapKey(&Joystick, H2D, SC_TARGET_HOSTILE_FWD); // Short
    // MapKey(&Joystick, H2D,  TARGET_NEAREST_LANDZONE); // Long
    // TODO: Fwd/Back modified by LASTE switch ---------------------------------
    //       PATH    - Friendlies
    //       ALT/HDG - All
    //       ALT     - Hostile
    // -------------------------------------------------------------------------
    MapKey(&Joystick, H2L, SC_TARGET_ALL_BACK);
    MapKey(&Joystick, H2R, SC_TARGET_ALL_FWD);
    
    // DMS Hat
    // Modified by EAC switch --------------------------------------------------
    //       ARM - Power Management
    //       OFF - Missile Select
    // -------------------------------------------------------------------------
    MapKey(&Joystick, H3U, EXEC("power_selector();"));
    MapKey(&Joystick, H3D, EXEC("power_selector();"));
    MapKey(&Joystick, H3L, EXEC("power_selector();"));
    MapKey(&Joystick, H3R, EXEC("power_selector();"));
    
    MapKeyR(&Joystick, H3U, EXEC("power_selector();"));
    MapKeyR(&Joystick, H3D, EXEC("power_selector();"));
    MapKeyR(&Joystick, H3L, EXEC("power_selector();"));
    MapKeyR(&Joystick, H3R, EXEC("power_selector();"));
    
    MapKey(&Throttle, EACON, EXEC("power_selector();"));
    MapKey(&Throttle, EACOFF, EXEC("power_selector();"));
    
    // CMS Hat
    MapKey(&Joystick, H4U, SC_LAUNCH_COUNTERMEASURE);
    MapKey(&Joystick, H4D, SC_COUNTERMEASURE_CYCLE_FWD);
    MapKey(&Joystick, H4L, SC_RADAR_ZOOM);
    MapKey(&Joystick, H4R, SC_RADAR_ZOOM);
    // MapKey(&Joystick, H4P, RADAR_ACTIVE_PASSIVE); // Short
    // MapKey(&Joystick, H4P, RADAR_ON_OFF); // Long
    
    // ----------------
    // Throttle Buttons
    // ----------------
    
    // Coolie Hat
    MapKey(&Throttle, CSU, EXEC("coolie_hat_selector();"));
    MapKey(&Throttle, CSD, EXEC("coolie_hat_selector();"));
    MapKey(&Throttle, CSL, EXEC("coolie_hat_selector();"));
    MapKey(&Throttle, CSR, EXEC("coolie_hat_selector();"));
    
    MapKeyR(&Throttle, CSU, EXEC("coolie_hat_selector();"));
    MapKeyR(&Throttle, CSD, EXEC("coolie_hat_selector();"));
    MapKeyR(&Throttle, CSL, EXEC("coolie_hat_selector();"));
    MapKeyR(&Throttle, CSR, EXEC("coolie_hat_selector();"));
    
    MapKey(&Throttle, BSF, EXEC("coolie_hat_mode_changed();"));
    MapKey(&Throttle, BSM, EXEC("coolie_hat_mode_changed();"));
    MapKey(&Throttle, BSB, EXEC("coolie_hat_mode_changed();"));
    
    // Mic Switch
    MapKey(&Throttle, MSU, SC_SHIELD_FWD);
    MapKey(&Throttle, MSD, SC_SHIELD_AFT);
    MapKey(&Throttle, MSL, SC_SHIELD_LEFT);
    MapKey(&Throttle, MSR, SC_SHIELD_RIGHT);
    
    MapKey(&Throttle, MSP, SC_SHIELD_RESET);
    
    // Slew Control Button
    MapKey(&Throttle, SC, SC_IFCS_DECOUPLE);
    
    // Speed SC_Brake
    MapKey(&Throttle, SPDF, SC_AFTERBURNER);
    MapKey(&Throttle, SPDB, SC_BRAKE);
    
    // China Hat
    MapKey(&Throttle, CHF, SC_AIM_RESET);
    MapKey(&Throttle, CHB, SC_GIMBAL_LOCK);
    
    // Left Throttle Button
    MapKey(&Throttle, LTB, VOICE_ATTACK);
    
    // Landing Gear Horn Silence
    MapKey(&Throttle, LDGH, SC_EJECT);
    
    // Autopilot Engage (Black Button On Base)
    MapKey(&Throttle, APENG, SC_USE_DOCKING);
    
    // ----------------
    // Left MFD Buttons
    // ----------------
    
    // MapKey(&LMFD, OSB01, );
    // MapKey(&LMFD, OSB02, );
    // MapKey(&LMFD, OSB03, RADAR_ACTIVE_PASSIVE);
    // MapKey(&LMFD, OSB04, );
    // MapKey(&LMFD, OSB05, );
    
    // MapKey(&LMFD, OSB06, RADAR_ON_OFF);
    // MapKey(&LMFD, OSB07, );
    // MapKey(&LMFD, OSB08, SHIELD_GEN_ON_OFF);
    // MapKey(&LMFD, OSB09, );
    // MapKey(&LMFD, OSB10, TARGETING_CPU_ON_OFF);
    
    // MapKey(&LMFD, OSB11, );
    // MapKey(&LMFD, OSB12, );
    MapKey(&LMFD, OSB13, SC_RUNNING_LIGHTS);
    // MapKey(&LMFD, OSB14, );
    MapKey(&LMFD, OSB15, SC_CABIN_LIGHTS);
    
    // MapKey(&LMFD, OSB16, );
    // MapKey(&LMFD, OSB17, );
    MapKey(&LMFD, OSB18, SC_SHIELD_BOOST_RECHARGE);
    // MapKey(&LMFD, OSB19, );
    MapKey(&LMFD, OSB20, SC_USE_DOCKING);
    
    MapKey(&LMFD, SYMU, SC_RADAR_ZOOM);
    MapKey(&LMFD, SYMD, SC_RADAR_ZOOM);
    // MapKey(&LMFD, CONU,  );
    // MapKey(&LMFD, COND,  );
    // MapKey(&LMFD, BRTU,  ); // Cycle Friendly Fwd
    // MapKey(&LMFD, BRTD,  ); // Cycle Friendly Back
    // MapKey(&LMFD, GAINU, RADAR_MODE_FWD);
    // MapKey(&LMFD, GAIND, RADAR_MOODE_BACK);
    
    // -----------------
    // Right MFD Buttons
    // -----------------
    
    MapKey(&RMFD, OSB01, SC_HUD_TAB_1);
    MapKey(&RMFD, OSB02, SC_HUD_TAB_2);
    MapKey(&RMFD, OSB03, SC_HUD_TAB_3);
    MapKey(&RMFD, OSB04, SC_HUD_TAB_4);
    MapKey(&RMFD, OSB05, SC_HUD_TAB_5);
    
    // MapKey(&RMFD, OSB06, HUD_CANCEL);
    // MapKey(&RMFD, OSB07, );
    MapKey(&RMFD, OSB08, SC_HUD_MAXIMIZE);
    // MapKey(&RMFD, OSB09, );
    // MapKey(&RMFD, OSB10, );
    
    MapKey(&RMFD, OSB11, SC_HUD_TAB_10);
    MapKey(&RMFD, OSB12, SC_HUD_TAB_9);
    MapKey(&RMFD, OSB13, SC_HUD_TAB_8);
    MapKey(&RMFD, OSB14, SC_HUD_TAB_7);
    MapKey(&RMFD, OSB15, SC_HUD_TAB_6);
    
    MapKey(&RMFD, OSB16, SC_SCOREBOARD);
    // MapKey(&RMFD, OSB17, );
    // MapKey(&RMFD, OSB18, HUD_INTERACT);
    // MapKey(&RMFD, OSB19, );
    MapKey(&RMFD, OSB20, SC_HUD_CONFIRM);
    
    MapKey(&RMFD, SYMU,  SC_HUD_LEFT_PANEL_UP);
    MapKey(&RMFD, SYMD,  SC_HUD_LEFT_PANEL_DOWN);
    MapKey(&RMFD, CONU,  SC_HUD_LEFT_PANEL_LEFT);
    MapKey(&RMFD, COND,  SC_HUD_LEFT_PANEL_RIGHT);
    // MapKey(&RMFD, BRTU, );
    // MapKey(&RMFD, BRTD, );
    MapKey(&RMFD, GAINU, SC_HUD_MODE_FWD);
    MapKey(&RMFD, GAIND, SC_HUD_MODE_BACK);
}

int EventHandle(int type, alias o, int x)
{
    DefaultMapping(&o, x);
}

int set_tg1_weapon(int weapon)
{
    // TODO: Bug:
    //          When TG1 = TG2. Both active:
    //              Moving TG1 or TG2 causes the opposite to deactivate.
    // if (Joystick[TG1] & (tg1_weapon != weapon)) ActKey(tg1_weapon);
    
    if (Joystick[TG1]) ActKey(tg1_weapon);
    tg1_weapon = weapon;
    if (Joystick[TG1]) ActKey(KEYON + tg1_weapon);
    update_leds();
}

int set_tg2_weapon(int weapon)
{
    if (Joystick[TG2] & (tg2_weapon != weapon)) ActKey(tg2_weapon);
    tg2_weapon = weapon;
    if (Joystick[TG2]) ActKey(KEYON + tg2_weapon);
    update_leds();
}

int swap_weapons()
{
    int temp_weapon = tg1_weapon;
    tg1_weapon = tg2_weapon;
    tg2_weapon = temp_weapon;
    update_leds();
    weapon_selector();
}

int weapon_selector() {
    if (Joystick[TG1]) ActKey(KEYON + tg1_weapon); else ActKey(tg1_weapon);
    
    if (tg1_weapon != tg2_weapon)
        if (Joystick[TG2]) ActKey(KEYON + tg2_weapon); else ActKey(tg2_weapon);
}

int update_leds()
{
    // Throttle LED's off.
    // This also causes a fast blink for UI feedback if the same weapon is
    // selected, or the TG2 weapon is selected.
    ActKey(PULSE + KEYON + LED(&Throttle, LED_ONOFF, LED_CURRENT - LED1));
    ActKey(PULSE + KEYON + LED(&Throttle, LED_ONOFF, LED_CURRENT - LED2));
    ActKey(PULSE + KEYON + LED(&Throttle, LED_ONOFF, LED_CURRENT - LED3));
    ActKey(PULSE + KEYON + LED(&Throttle, LED_ONOFF, LED_CURRENT - LED4));
    ActKey(PULSE + KEYON + LED(&Throttle, LED_ONOFF, LED_CURRENT - LED5));
    
    if (tg1_weapon == SC_WEAPON_GROUP_1)
        ActKey(PULSE + KEYON + LED(&Throttle, LED_ONOFF, LED_CURRENT + LED1));
    else if (tg1_weapon == SC_WEAPON_GROUP_2)
        ActKey(PULSE + KEYON + LED(&Throttle, LED_ONOFF, LED_CURRENT + LED2));
    else if (tg1_weapon == SC_WEAPON_GROUP_3)
        ActKey(PULSE + KEYON + LED(&Throttle, LED_ONOFF, LED_CURRENT + LED3));
    else if (tg1_weapon == SC_WEAPON_GROUP_4)
        ActKey(PULSE + KEYON + LED(&Throttle, LED_ONOFF, LED_CURRENT + LED4));
}

int power_selector()
{
    if (Throttle[EACON])
    {
        // Power Management
        if (Joystick[H3U]) ActKey(KEYON + SC_POWER_GROUP_RESET); else ActKey(SC_POWER_GROUP_RESET);
        if (Joystick[H3D]) ActKey(KEYON + SC_POWER_GROUP_3); else ActKey(SC_POWER_GROUP_3);
        if (Joystick[H3L]) ActKey(KEYON + SC_POWER_GROUP_1); else ActKey(SC_POWER_GROUP_1);
        if (Joystick[H3R]) ActKey(KEYON + SC_POWER_GROUP_2); else ActKey(SC_POWER_GROUP_2);
    }
    else
    {
        // Missile Management
        // if (Joystick[H3U]) ActKey(KEYON+DX11); else ActKey(DX11); // Dynamic Focus
        // if (Joystick[H3D]) ActKey(KEYON+DX13); else ActKey(DX13);
        if (Joystick[H3L]) ActKey(KEYON + SC_MISSILE_CYCLE_BACK); else ActKey(SC_MISSILE_CYCLE_BACK);
        if (Joystick[H3R]) ActKey(KEYON + SC_MISSILE_CYCLE_FWD); else ActKey(SC_MISSILE_CYCLE_FWD);
    }
}

int coolie_hat_selector()
{
    if (Throttle[BSF])
    {
        // Strafing
        if (Throttle[CSU]) ActKey(KEYON + SC_STRAFE_UP); else ActKey(SC_STRAFE_UP);
        if (Throttle[CSD]) ActKey(KEYON + SC_STRAFE_DOWN); else ActKey(SC_STRAFE_DOWN);
        if (Throttle[CSL]) ActKey(KEYON + SC_STRAFE_LEFT); else ActKey(SC_STRAFE_LEFT);
        if (Throttle[CSR]) ActKey(KEYON + SC_STRAFE_RIGHT); else ActKey(SC_STRAFE_RIGHT);
    }
    else if (Throttle[BSM])
    {
        // HUD Menu Selector
        if (Throttle[CSU]) ActKey(KEYON + SC_HUD_OVERVIEW); else ActKey(SC_HUD_OVERVIEW);
        if (Throttle[CSD]) ActKey(KEYON + SC_HUD_SHIELDS); else ActKey(SC_HUD_SHIELDS);
        if (Throttle[CSL]) ActKey(KEYON + SC_HUD_WEAPONS); else ActKey(SC_HUD_WEAPONS);
        if (Throttle[CSR]) ActKey(KEYON + SC_HUD_POWER); else ActKey(SC_HUD_POWER);
    }
}

int coolie_hat_mode_changed()
{
    // Release keys.
    ActKey(SC_STRAFE_UP);
    ActKey(SC_STRAFE_DOWN);
    ActKey(SC_STRAFE_LEFT);
    ActKey(SC_STRAFE_RIGHT);
    
    ActKey(SC_HUD_OVERVIEW);
    ActKey(SC_HUD_SHIELDS);
    ActKey(SC_HUD_WEAPONS);
    ActKey(SC_HUD_POWER);
    
    // Update active button.
    coolie_hat_selector();
}
